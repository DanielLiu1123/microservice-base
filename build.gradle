plugins{
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id "com.google.protobuf" version "${protobufPluginVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}" apply false
    id 'com.diffplug.spotless' version "${spotlessVersion}" apply false
    id "com.github.spotbugs" version "${spotbugsVersion}" apply false
}

subprojects  {
    apply plugin: 'java-library'
    repositories {
        mavenLocal()
        mavenCentral()
    }
    test {
        useJUnitPlatform()
    }
    // spotless
    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()
            targetExclude 'build/generated/**/*.java'
        }
    }
    // spotbugs
    apply plugin: 'com.github.spotbugs'
    spotbugs {
        spotbugsTest.enabled = false
        omitVisitors = ['FindReturnRef', 'MethodReturnCheck']
    }
}